'''
Copyright (c) 2008-2014, Pedigree Developers

Please see the CONTRIB file in the root of the source tree for a full
list of contributors.

Permission to use, copy, modify, and distribute this software for any
purpose with or without fee is hereby granted, provided that the above
copyright notice and this permission notice appear in all copies.

THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
'''

# vim: set filetype=python:

import os
import SCons

from buildutils import fs, db
from buildutils.diskimages import build

Import(['env', 'host_env'])
if not env['build_tests_only']:
  Import(['userspace_env'])

BUILDDIR = env['BUILDDIR']

def path2sconscript(*args):
  d = BUILDDIR.Dir(args[0])
  for p in args[1:]:
    d = d.Dir(p)
  return d.File('SConscript')

# Host utilities or libraries for host tools in git submodules.
SConscript(path2sconscript('external'), exports=['host_env'])

# Build utilities that run on the host during the kernel build.
SConscript(path2sconscript('buildutil'), exports=['host_env'])

if env['build_modules']:
  # POSIX subsystem.
  SConscript(path2sconscript('subsys', 'posix'),
             exports=['env', 'userspace_env'])
  # Pedigree-C subsystem.
  SConscript(path2sconscript('subsys', 'pedigree-c'),
             exports=['env', 'userspace_env'])
  # Native subsystem.
  if 'NATIVE_SUBSYSTEM' in env['EXTRA_CONFIG']:
      SConscript(path2sconscript('subsys', 'native'),
                 exports=['env', 'userspace_env'])
  # Kernel drivers and modules.
  SConscript(path2sconscript('modules'), exports=['env'])

if env['build_kernel']:
  SConscript(path2sconscript('system', 'kernel'), exports=['env'])
  if env['BOOT_DIR']:
    SConscript(path2sconscript('system', 'boot', env['BOOT_DIR']), exports=['env'])

# On X86, X64 and PPC we build applications and LGPL libraries
if env['build_apps'] or env['build_libs']:
  SConscript(path2sconscript('user'),
             exports=['env', 'userspace_env'])
if env['build_lgpl']:
  SConscript(path2sconscript('lgpl'),
             exports=['env', 'userspace_env'])

# Build the configuration database (no dependencies)
config_database = env["PEDIGREE_BUILD_BASE"].File('config.db')
if env['build_configdb']:
  configSchemas = fs.find_files(env.Dir('.').srcnode().abspath,
                                lambda x: x == 'schema', [])
  env.Sqlite(config_database, configSchemas)

  if 'STATIC_DRIVERS' in env['CPPDEFINES']:
      # Generate config database header file for static inclusion.
      config_header = env.File('modules/system/config/config_database.h')
      env.FileAsHeader(config_header, config_database)

# Should we build translations?
if env['build_translations']:
  # Note: SConscript.i18n is NOT built in the VariantDir as we want to take
  # the generate POT and PO files and commit them to source control. The other
  # SConscript generates MO files, which should NOT be in source control.
  SConscript(path2sconscript('po'), exports=['env'])

# Build disk images.
if env['build_images']:
  build.buildDiskImages(env, config_database)

# Pyflakes - linting python files in the tree.
rootdir = env.Dir("#").abspath
imagesroot = env.Dir("#images").abspath

if env['pyflakes'] or env['sconspyflakes']:
    # Find .py files in the tree, excluding the images directory.
    pyfiles = fs.find_files(rootdir, lambda x: x.endswith('.py'), [imagesroot])
    pyflakes = env.Pyflakes('pyflakes-result', pyfiles)
    env.AlwaysBuild(pyflakes)
