#!/usr/bin/env python2.7
'''
Copyright (c) 2008-2014, Pedigree Developers

Please see the CONTRIB file in the root of the source tree for a full
list of contributors.

Permission to use, copy, modify, and distribute this software for any
purpose with or without fee is hereby granted, provided that the above
copyright notice and this permission notice appear in all copies.

THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
'''

import os

import buildutils.userspace
import buildutils.misc

Import(['userspace_env'])

env = userspace_env

env_clone = env.Clone()

appsbase = env.Dir('applications')
appoutputdir = env["PEDIGREE_BUILD_APPS"]
imagesdir = env["PEDIGREE_IMAGES_DIR"]
builddir = env["PEDIGREE_BUILD_BASE"]

suffix = buildutils.misc.stubSuffix(userspace_env)

EXTERNAL_LIBRARY_DIR = '#images/local/libraries'
EXTERNAL_INCLUDE_DIR = '#images/local/include'
if env['ON_PEDIGREE']:
    EXTERNAL_LIBRARY_DIR = '/libraries'
    EXTERNAL_INCLUDE_DIR = '/include'

# libloader (userspace dynamic linker).
libload_sources = [
    'loader.cc',
    'stubs%s.s' % suffix,
]

# Applications which just use POSIX libc/libm.
posixc_apps = [
    'cdhelp',
    'crashtest',
    'display-mode',
    'gears',
    'init',
    'keymap',
    'login',
    'modlist',
    'modload',
    'modunload',
    'mount',
    'net-test',
    'nyancat',
    'preloadd',
    'reboot',
    'sudo',
    'syscall-test',
    'testsuite',
]

# Applications which use gettext.
gettext_apps = [
    'login',
    'live',
    'tour',
]

# Applications which use Mesa (affects libraries & headers).
mesa_apps = [
    'gears',
]

# Applications which use libSDL (affects libraries & headers).
sdl_apps = [
    'fire'
]

# Applications which use libpedigree and/or libstdc++.
cpp_apps = [
    'apptest',
    'config',
    'ipc-test-client',
    'ipc-test-server',
    'live',
    'thread-test',
    'ttyterm',
    'TUI',
    'gfxcon',
    'uitest',
]

# Applications which require C++11.
cpp_11_apps = [
    'which',
    'winman',
    'tour',
]

# Applications which use libui (affects libraries & headers).
libui_apps = [
    'gears',
    'TUI',
    'gfxcon',
    'uitest',
]

# Applications which need pango for text rendering.
pango_apps = [
    'winman',
    'TUI',
    'gfxcon',
]

# Custom include paths for specific applications.
custom_incpath = {
    'TUI' : [EXTERNAL_INCLUDE_DIR,
             EXTERNAL_INCLUDE_DIR + '/freetype2',
             '#src/user/libraries/libtui/include',
    ],
    'gfxcon': [
        '#src/user/libraries/libtui/include',
        '#src/user/libraries/libfb/include',
    ],
    'winman' : ['#src/user/libraries/libui/include',
                '#src/user/libraries/libui/src',
                EXTERNAL_INCLUDE_DIR,
                EXTERNAL_INCLUDE_DIR + '/pixman-1',
                EXTERNAL_INCLUDE_DIR + '/freetype2',
                '#src/user/libraries/libfb/include']
}

# Custom libraries for specific applications
custom_libs = {
    'config': ['pedigree'],
    'ipc-test-client': ['pedigree'],
    'ipc-test-server': ['pedigree'],
    'thread-test' : ['pthread'],
    'ttyterm': ['pedigree'],
    'TUI' : ['tui', 'cairo', 'freetype', 'png', 'iconv', 'pedigree'],
    'gfxcon' : ['fb', 'tui', 'cairo', 'freetype', 'png', 'iconv', 'pedigree'],
    'uitest' : ['pedigree'],
    'winman': ['fb', 'cairo', 'freetype', 'png', 'pedigree'],
    'apptest': ['pedigree'],
    'live': ['dialog', 'ncurses', 'pedigree'],
    'login': ['pedigree-c'],
    'tour': ['dialog', 'ncurses'],
    'keymap': ['pedigree-c'],
    'modlist': ['pedigree-c'],
    'modload': ['pedigree-c'],
    'modunload': ['pedigree-c'],
    'mount': ['pedigree-c'],
    'sudo': ['pedigree-c'],
    'init': ['pedigree-c'],
}

# Applications to not enable -Werror for.
no_werror_apps = [
    'fire',  # C89.
    'gears',  # opengl
    'keymap',  # lex/yacc
    'nyancat',  # not ours
]

# Default libraries for all applications
base_libs = []

# Default include paths
if env['posix_musl']:
    base_include_dirs = [
        builddir.Dir('musl').Dir('include'),
        EXTERNAL_INCLUDE_DIR,
    ]
else:
    base_include_dirs = [
        EXTERNAL_INCLUDE_DIR,
    ]

# Flags from CXXFLAGS and CFLAGS to *remove* for an app
remove_flags = {
    'nyancat': ['-Wcast-qual'],
    'which': ['-Wwrite-strings'],
    'tour': ['-Wwrite-strings'],
}

# Extra flags to add to flags for an app.
add_flags = {
    # Both of these use getopt, which has a const-ness problem.
    'nyancat': ['-Wno-cast-qual', '-Wno-write-strings', '-Wno-clobbered'],
    'which': ['-Wno-cast-qual', '-Wno-write-strings'],
    'tour': ['-Wno-write-strings'],
}

env_clone['CFLAGS'] = buildutils.userspace.fixFlags(env, env['CFLAGS'])
env_clone['CCFLAGS'] = buildutils.userspace.fixFlags(env, env['CCFLAGS'])
env_clone['CXXFLAGS'] = buildutils.userspace.fixFlags(env, env['CXXFLAGS'])
env_clone['CPPDEFINES'] += ['PEDIGREE', '__PEDIGREE__']
env_clone['LIBS'] = base_libs
env_clone['LIBPATH'] = [builddir, builddir.Dir('libs')]
env_clone['CPPPATH'] = base_include_dirs

if env['posix_musl']:
    env_clone['LIBPATH'].append(builddir.Dir('musl').Dir('lib'))

env_clone.MergeFlags({
    'CPPDEFINES': ['_POSIX_SOURCE', '_POSIX_C_SOURCE=200112L', '_XOPEN_SOURCE=600'],
    'CCFLAGS': ['-Wno-undef', '-Wno-parentheses'],
})

if not env['ON_PEDIGREE']:
    cpp_base = env.Dir('#images/local/support/gcc/include/c++/%s' % env['CCVERSION'])
    cpp_arch_base = cpp_base.Dir(env['COMPILER_TARGET'])

# -static stops GCC placing the path to the dynamic linker into the binary, and creates a non-relocateable executable.
if env['build_libs'] and not env['posix_musl']:
    libload_env = env_clone.Clone()
    libload_env.MergeFlags('-static')

    libload_env['CPPPATH'] += ['#src/system/include/linker']
    if not env['ON_PEDIGREE']:
        libload_env['CPPPATH'] += [cpp_base, cpp_arch_base]

    libload_env['LIBPATH'] += [builddir, imagesdir + 'libraries']
    libload_env['LIBS'] = ['-lstdc++', '-lc', 'pedigree-c', '-lgcc']

    libload = os.path.join(builddir, 'libload.so')
    libload_linkscript = libload_env.File(os.path.join(appsbase, 'libload', 'libload%s.ld' % suffix))

    in_files = [os.path.join(appsbase, 'libload', x) for x in libload_sources]
    libload_env.Program(libload, in_files,
        LINKFLAGS="-static -Wl,-Bstatic -Wl,--no-whole-archive -nodefaultlibs -T%s " % libload_linkscript)
    libload_env.Depends(libload, "crt0")

    env.Alias('libload', libload)

apps = []

if env['build_apps']:
    apps += posixc_apps + mesa_apps + sdl_apps + cpp_apps + cpp_11_apps

if env['build_apps'] and env['build_libs']:
    apps += libui_apps

apps = set(apps)

# Clean up app dependencies as needed.
if not env['with_graphical_apps']:
    apps -= set(libui_apps + pango_apps + mesa_apps + sdl_apps)
    libui_apps = []
    pango_apps = []
    mesa_apps = []
    sdl_apps = []

for app in set(apps):
    app_env = env_clone.Clone()
    if env['hosted']:
        app_env['LSCRIPT'] = app_env.File('#src/system/kernel/core/processor/hosted/app.ld')
        app_env['LINKFLAGS'] = ['-T$LSCRIPT']
    else:
        app_env['LINKFLAGS'] = []

    base = appsbase.Dir(app)

    # Handle custom paths and libraries.
    custom_include_path = base.Dir('include')
    if custom_include_path.exists():
        app_env['CPPPATH'] += [custom_include_path]

    custom_include_path = custom_incpath.get(app)
    if custom_include_path:
        app_env['CPPPATH'] += custom_include_path

    custom_link_libs = custom_libs.get(app)
    if custom_link_libs:
        if 'pedigree' in custom_link_libs and 'pedigree-c' not in custom_link_libs:
            custom_link_libs.append('pedigree-c')
        app_env['LIBS'] += custom_link_libs

    # Handle SDL dependencies.
    if app in sdl_apps:
        # Don't build SDL apps if we don't have SDL.
        if not env['build_lgpl']:
            continue
        app_env['LIBS'] += ['SDL']
        app_env['CPPPATH'] += ['#src/lgpl/SDL-1.2.14/include',
                               EXTERNAL_INCLUDE_DIR]

    # Handle gettext dependency.
    if app in gettext_apps:
        app_env['LIBS'] += ['intl']
        app_env['LIBPATH'] += [EXTERNAL_LIBRARY_DIR]

    # Handle Mesa dependencies.
    if app in mesa_apps:
        app_env['LIBS'] += ['OSMesa']
        app_env['LIBPATH'] += [EXTERNAL_LIBRARY_DIR]
        app_env['CPPPATH'] += [EXTERNAL_INCLUDE_DIR]

    # Handle C++-related dependencies.
    if app in set(cpp_apps + sdl_apps + mesa_apps + cpp_11_apps):
        app_env['LIBPATH'] += [EXTERNAL_LIBRARY_DIR]
        if not env['ON_PEDIGREE']:
            app_env['CPPPATH'] += [cpp_base, cpp_arch_base]
        app_env['LINK'] = env['CXX']

    # Handle libui-related dependencies.
    if app in libui_apps:
        app_env['CPPPATH'] += ['#src/user/libraries/libui/include']
        app_env['LIBS'] += ['ui']

    if app in pango_apps:
        app_env['LIBPATH'] += [EXTERNAL_LIBRARY_DIR]
        app_env['CPPPATH'] += [
            EXTERNAL_LIBRARY_DIR + '/glib-2.0/include',
            EXTERNAL_INCLUDE_DIR + '/glib-2.0',
            EXTERNAL_INCLUDE_DIR + '/pango-1.0',
            EXTERNAL_INCLUDE_DIR + '/cairo',
        ]

        # pango pulls in a lot of dependencies.
        app_env['LIBS'] += ['cairo', 'pangocairo-1.0', 'pangoft2-1.0',
            'pango-1.0', 'harfbuzz', 'fontconfig', 'gobject-2.0', 'glib-2.0',
            'ffi']

    app_env['LINKFLAGS'] += ['-Wl,-rpath-link,%s' % (
        app_env.Dir(builddir).abspath,)]
    app_env['LINKFLAGS'] += ['-Wl,-rpath-link,%s/libs/' % (
        app_env.Dir(builddir).abspath,)]
    app_env['LINKFLAGS'] += ['-Wl,-rpath-link,%s/libraries/' % (
        app_env.Dir(imagesdir).abspath,)]

    if app in cpp_11_apps:
        try:
            app_env['CXXFLAGS'].remove('-std=gnu++98')
        except ValueError:
            pass  # Not found.
        app_env.MergeFlags({'CXXFLAGS': '-std=gnu++11'})

    buildutils.misc.removeFromAllFlags(app_env, set(remove_flags.get(app, [])))

    if app in no_werror_apps:
        buildutils.misc.removeFromAllFlags(app_env, '-Werror')

    app_env.MergeFlags({'CCFLAGS': add_flags.get(app, [])})

    output = builddir.Dir('apps').File(app)
    files = base.glob('*.c') + base.glob('*.cc')

    output_bin = app_env.Program(output, files)

    env.Alias("apps", output_bin)

    if 'LSCRIPT' in app_env:
        app_env.Depends(output_bin, app_env['LSCRIPT'])

    app_env.Depends(output_bin, env.Alias('libs'))
    app_env.Requires(output_bin, env.Alias('libload'))
    app_env.Depends(output_bin, env.Alias('headers'))
    app_env.Depends(output_bin, env.Alias('native-headers'))

if env['build_libs']:
    SConscript(os.path.join('libraries', 'SConscript'), exports=['env'])
