'''
Copyright (c) 2008-2014, Pedigree Developers

Please see the CONTRIB file in the root of the source tree for a full
list of contributors.

Permission to use, copy, modify, and distribute this software for any
purpose with or without fee is hereby granted, provided that the above
copyright notice and this permission notice appear in all copies.

THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
'''

import os
import platform

Import(['env'])

netwrap_bin = env['BUILDDIR'].File('netwrap')
tcpecho_bin = env['BUILDDIR'].File('tcpecho')
libnetwork = 'pedigree-network'
libnetstack = 'pedigree-netstack'
libconfig = 'pedigree-config'
liblwip = 'lwip'

build_env = env.Clone()

# Set up includes correctly.
header_paths = [
    env.Dir('#src'),
    env.Dir('#src/modules'),
    env.Dir('#src/modules/system'),
    env.Dir('#src/modules/system/lwip/include'),
    env.Dir('#src/system/include'),
    env.Dir('#src/system/kernel/core'),
]

build_env.Append(CPPPATH=header_paths)

# Create a fake build environment.
defines = [
    'HOSTED', 'DEVICE_IGNORE_ADDRESSES', 'TARGET_IS_LITTLE_ENDIAN',
    'MEMORY_TRACING', 'STATIC_DRIVERS', 'STANDALONE_MUTEXES', 'UTILITY_LINUX',
    'DISABLE_RAWNET', 'NO_NETWORK_SERVICES', 'STANDALONE_MEMPOOL'
]

build_env.Append(CPPDEFINES=defines)


def filtered_flag(x):
    return x.startswith('_POSIX_C_SOURCE') or x.startswith('_XOPEN_SOURCE')

# sqlite needs to not have POSIX_C_SOURCE and others as that breaks the build
# on OSX - certain types don't get correctly defined.
sqlite_cppdefines = [x for x in build_env['CPPDEFINES'] if not filtered_flag(x)]

build_env.MergeFlags({
    'CXXFLAGS': ['-std=c++11', '-Os'],
    'CCFLAGS': ['-g3', '-ggdb'],
    'LINKFLAGS': ['-g3', '-ggdb'],
    'LIBS': ['kernel', 'utility', 'pthread', 'dl', 'rt'],
    'LIBPATH': ['..'],  # buildutil, parent libraries
})

libconfig_sources = [
    'config-shim.cc',
    '../../modules/system/config/Config.cc',
    '../../modules/system/config/Config.h',
    '../../modules/system/config/sqlite3/sqlite3.c',
]

libnetwork_sources = [
    '../../system/kernel/machine/Network.cc',
]

libnetstack_sources = [
    '../../modules/system/network-stack/TcpMisc.cc',
    '../../modules/system/network-stack/ConnectionBasedEndpoint.cc',
    # '../../modules/system/network-stack/RawManager.cc',
    '../../modules/system/network-stack/Ipv4.cc',
    '../../modules/system/network-stack/IpCommon.cc',
    '../../modules/system/network-stack/TcpEndpoint.cc',
    '../../modules/system/network-stack/Ndp.cc',
    '../../modules/system/network-stack/Udp.cc',
    '../../modules/system/network-stack/TcpManager.cc',
    '../../modules/system/network-stack/Ethernet.cc',
    '../../modules/system/network-stack/RoutingTable.cc',
    '../../modules/system/network-stack/Filter.cc',
    '../../modules/system/network-stack/Endpoint.cc',
    '../../modules/system/network-stack/Icmp.cc',
    '../../modules/system/network-stack/NetworkStack.cc',
    '../../modules/system/network-stack/TcpManagerReceive.cc',
    '../../modules/system/network-stack/UdpLogger.cc',
    '../../modules/system/network-stack/Arp.cc',
    '../../modules/system/network-stack/Icmpv6.cc',
    '../../modules/system/network-stack/Tcp.cc',
    '../../modules/system/network-stack/Ipv6.cc',
    '../../modules/system/network-stack/NeighbourDiscovery.cc',
    '../../modules/system/network-stack/NetManager.cc',
    '../../modules/system/network-stack/UdpManager.cc',
    '../../modules/system/network-stack/TcpStateBlock.cc',
]

liblwip_sources = [
    Glob('../../modules/system/lwip/*.c'),
    Glob('../../modules/system/lwip/*.cc'),
    Glob('../../modules/system/lwip/core/*.c'),
    Glob('../../modules/system/lwip/core/ipv4/*.c'),
    Glob('../../modules/system/lwip/core/ipv6/*.c'),
    Glob('../../modules/system/lwip/api/*.c'),
    Glob('../../modules/system/lwip/netif/*.c'),
]

config = build_env.Library(libconfig, libconfig_sources, CPPDEFINES=sqlite_cppdefines)
network = build_env.Library(libnetwork, libnetwork_sources)
netstack = build_env.Library(libnetstack, libnetstack_sources)
lwip = build_env.Library(liblwip, liblwip_sources)

build_env['LIBS'] = [netstack, network, config, lwip] + build_env['LIBS']

# The actual netwrap binary only works on Linux.
if platform.system() == 'Linux':
    build_env.Program(netwrap_bin, ['main.cc', 'TunWrapper.cc'])
    build_env.Program(tcpecho_bin, ['tcpecho.cc', 'TunWrapper.cc'])
